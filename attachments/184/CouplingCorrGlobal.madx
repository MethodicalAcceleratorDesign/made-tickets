!  Global Correction of the Coupling introduced by the cooler
!    In addition to local (around cooler) magnets, rematch the all
!    Quadrupole Power Supplies !

!  Apply Procedure by J. Pasternak to match iteratively :
!     -  coupling compensation and 
!     -  re-match "normal" lattice params

  Niter = 4;

  WHILE (Niter > 0)
  { MATCH, RMATRIX;  ! First match coupling correction 
      CONSTRAINT, RANGE=CtrS30[1], RE13=0., RE14=0., RE23=0., RE24=0.; 
      CONSTRAINT, RANGE=CtrS20[1], X=0, PX=0., Y=0., PY=0.;
      CONSTRAINT, RANGE=CtrS40[1], X=0, PX=0., Y=0., PY=0.; 
      VARY, NAME = kSk1,   STEP=0.01;
      VARY, NAME = MsolC,  STEP=0.01;
      VARY, NAME = DWHV12->HKICK, STEP=0.01;
      VARY, NAME = DWHV12->VKICK, STEP=0.01;
      VARY, NAME = DEHV21->HKICK, STEP=0.01;
      VARY, NAME = DEHV21->VKICK, STEP=0.01;
      VARY, NAME = DEHV22->HKICK, STEP=0.01;
      VARY, NAME = DEHV22->VKICK, STEP=0.01;
      VARY, NAME = DWHV21->HKICK, STEP=0.01;
      VARY, NAME = DWHV21->VKICK, STEP=0.01;
      LMDIF, CALLS=5000, TOLERANCE=1.0E-18;
    ENDMATCH;  

    MATCH;  ! Then match the rest of the lattice params (tunes, betas, ...)
      WEIGHT, BETX=.2, BETY=.2;
      CONSTRAINT, RANGE=CtrS40[1], BETX=BetHEC, BETY=BetVEC;  !, DX=0.
      WEIGHT, ALFX=1., ALFY=4.;
      CONSTRAINT, RANGE=CtrS10[1], ALFX=0., ALFY=0.;
      WEIGHT, BETX=100., BETY=100., DX=10.;
      CONSTRAINT, RANGE=CtrS20[1], BETX=BetHEC, BETY=BetVEC, DX=DisEC;
      GWEIGHT, Q1 = 10000., Q2 = 10000.;
      GLOBAL, Q1 = TuneH, Q2 = TuneV;
      GWEIGHT, dQ1=100., dQ2=100.;
      GLOBAL, dQ1 = dTuneHp, dQ2 = dTuneVp;
      VARY, NAME = kD1030, STEP=0.01;
      VARY, NAME = kF1030, STEP=0.01;
      VARY, NAME = kF2040, STEP=0.01;
      VARY, NAME = kD2040, STEP=0.01;
      VARY, NAME = kF2344, STEP=0.01;
      VARY, NAME = dkFT20, STEP=0.01;
      VARY, NAME = dkDT20, STEP=0.01;
      VARY, NAME = dkFT23, STEP=0.01;
      VARY, NAME = sD1030, STEP=0.01;
      VARY, NAME = sF1030, STEP=0.01;
      LMDIF, CALLS=5000, TOLERANCE=1.0E-18;
    ENDMATCH;

    Niter = Niter-1;
  }

  RETURN; 
