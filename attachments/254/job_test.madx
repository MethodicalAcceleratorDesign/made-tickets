
! Links definitions
option, warn,info;

system,"ln -fns /afs/cern.ch/eng/lhc/optics/V6.503 db5";
! system,"ln -fns /afs/cern.ch/user/r/rdemaria/www/www/pprun1/inj_flat result";
system,"mkdir -p result"

! Sequence definition
Option, -echo,warn,-info;

call, file="db5/V6.5.seq";

! Aperture definition

call, file="db5/aperture/aperture.b1.madx";
call, file="db5/aperture/aperture.b2.madx";
call, file="db5/aperture/aper_tol.b1.madx";
call, file="db5/aperture/aper_tol.b2.madx";


! Beam definition
beam, sequence=lhcb1, bv= 1, energy= 450,  particle=proton,
  npart=2,kbunch=5.00e10, ex=7.29555555556e-09,ey=7.29555555556e-09;
beam, sequence=lhcb2, bv=-1, energy= 450,  particle=proton,
  npart=2,kbunch=5.00e10, ex=7.29555555556e-09,ey=7.29555555556e-09;



! Strength definition

call, file="db5/V6.5.inj_special.str";

! Knowbs definitions

on_x1:=0; on_sep1:=0; on_o1:=0;
on_x2:=0; on_sep2:=0; on_o2:=0;
on_x5:=0; on_sep5:=0; on_o5:=0;
on_x8:=0; on_sep8:=0; on_o8:=0;
on_alice:=0;
on_lhcb :=0;


set,format="22.15e";
select,flag=twiss,clear;
select,flag=twiss,column=name,s,l,angle,k1l,k2l,k3l,hkick,vkick,kick,betx,bety,alfx,alfy,dx,dpx,dy,dpy,mux,muy,x,y,px,py,wx,wy,phix,phiy,n1,ddx,ddy,ddpx,ddpy;

if ( qxb1 == 0 ) {
  qxb1=qx1; qxb2=qx2; qyb1=qy1; qyb2=qy2;
  Qpxb1=qprime; Qpyb1=qprime; Qpxb2=qprime; Qpyb2=qprime;
};



! Aperture calculations

mk_apir(nir,bim) : macro ={

seqedit,sequence=lhcbim;flatten;cycle,start=s.ds.lnir.bim;endedit;

value,COmax,dPmax,emittance_norm,apbbeat,halor,halox,haloy,DParcx,DParcy;
exec,adjsector(nir);
exec,select(nir,$sec1,$sec2,bim);


use,sequence=lhcbim;
select,flag=twiss,clear;
select,flag=twiss,column=name,s,l,angle,k1l,k2l,hkick,vkick,kick,betx,bety,dx,dy,x,y,px,py,apertype,aper_1,aper_2,aper_3,aper_4,rtol,xtol,ytol,
       range=s.ds.lnir.bim/e.ds.rnir.bim;
twiss,file=result/twiss_irnirbim.tfs;

system, "cp db5/aperture/offset.ip*b*.tfs  .";
system, "cp slhc/aperture/offset.ip*b*.tfs .";

select,flag=aperture,clear;
select,flag=aperture,column=name,s,betx,bety,dx,dy,x,y,px,py,n1,apertype,Rtol,xtol,ytol,k1l,k1sl,k2l,k2sl,k3l,k3sl;
aperture,range=s.ds.lnir.bim/e.ds.rnir.bim,
         offsetelem=./offset.IPnir.bim.tfs,
         cor=COmax, dp=dPmax, spec=SPECIF,interval=1.0,
         exn=emittance_norm,eyn=emittance_norm,
         halo={6,halor,halox,haloy},bbeat=apbbeat,
         dparx=DParcx,dpary=DParcy,
         file=result/ap_irnirbim.tfs;

};


mk_aparc(NARC1,NARC2,BIM) : macro ={

seqedit,sequence=lhcBIM;cycle,start=e.ds.rNARC1.BIM;endedit;
use,sequence=lhcBIM;
select,flag=twiss,clear;
select,flag=twiss,column=name,s,
       l,angle,k1l,k2l,hkick,vkick,kick,
       betx,bety,dx,dy,x,y,
       range=e.ds.rNARC1.BIM/s.ds.lNARC2.BIM;
twiss,file=result/twiss_arcNARC1NARC2BIM.tfs;

select,flag=aperture,clear;
select,flag=aperture,column=name,s,betx,bety,dx,dy,x,y,n1,apertype,Rtol,xtol,ytol;
aperture,range=e.ds.rNARC1.BIM/s.ds.lNARC2.BIM,
         cor=COmax, dp=dPmax, spec=SPECIF,interval=1.0,
         file=ap_arcNARC1NARC2BIM.tfs;

};

select(NIR,sec1,sec2,BIM): macro = {

use,sequence=lhcBIM,range=s.cell.sec1.BIM/e.cell.sec1.BIM;
savebeta,label=baux,place=s.cell.sec1.BIM;
twiss;

use,sequence=lhcBIM,range=e.cell.sec1.BIM/s.ds.lNIR.BIM;
savebeta,label=birNIRBIM,place=s.ds.lNIR.BIM;
twiss,beta0=baux;
birNIRBIM->mux=0;birNIRBIM->muy=0;

use,sequence=lhcBIM,range=s.cell.sec2.BIM/e.cell.sec2.BIM;
savebeta,label=baux,place=s.cell.sec2.BIM;
twiss;
betxaux=baux->betx;betyaux=baux->bety;
alfxaux=baux->alfx;alfyaux=baux->alfy;
dxaux=baux->dx;dpxaux=baux->dpx;

use,sequence=lhcBIM,range=e.ds.rNIR.BIM/s.cell.sec2.BIM;
savebeta,label=eirNIRBIM,place=e.ds.rNIR.BIM;
twiss,rmatrix,betx=1,bety=1;
r11=table(twiss,s.cell.sec2.BIM,Re11);r12=table(twiss,s.cell.sec2.BIM,Re12);
r21=table(twiss,s.cell.sec2.BIM,Re21);r22=table(twiss,s.cell.sec2.BIM,Re22);
r33=table(twiss,s.cell.sec2.BIM,Re33);r34=table(twiss,s.cell.sec2.BIM,Re34);
r43=table(twiss,s.cell.sec2.BIM,Re43);r44=table(twiss,s.cell.sec2.BIM,Re44);
r16=table(twiss,s.cell.sec2.BIM,Re16);r26=table(twiss,s.cell.sec2.BIM,Re26);
eirNIRBIM->betx=r22^2*betxaux+2.*r22*r12*alfxaux+r12^2*(1.+alfxaux^2)/betxaux;
eirNIRBIM->alfx=r22*r21*betxaux+(r11*r22+r12*r21)*alfxaux+r12*r11*(1.+alfxaux^2)/betxaux;
eirNIRBIM->bety=r44^2*betyaux+2.*r44*r34*alfyaux+r34^2*(1.+alfyaux^2)/betyaux;
eirNIRBIM->alfy=r44*r43*betyaux+(r33*r44+r34*r43)*alfyaux+r34*r33*(1.+alfyaux^2)/betyaux;
eirNIRBIM->dx=r22*(dxaux-r16)-r12*(dpxaux-r26);
eirNIRBIM->dpx=-r21*(dxaux-r16)+r11*(dpxaux-r26);

!use,sequence=lhcBIM,range=e.ds.rNIR.BIM/s.cell.sec2.BIM;
!savebeta,label=baux,place=s.cell.sec2.BIM;
!twiss,beta0=eirNIRBIM;
!use,sequence=lhcBIM,range=s.cell.sec2.BIM/e.cell.sec2.BIM;
!savebeta,label=baux1,place=s.cell.sec2.BIM;
!twiss;
!value,baux->betx-baux1->betx,baux->bety-baux1->bety;
!value,baux->alfx-baux1->alfx,baux->alfy-baux1->alfy;
!value,baux->dx-baux1->dx,baux->dpx-baux1->dpx;

!show,birNIRBIM;
!show,eirNIRBIM;

};

adjsector(NIR): macro = {

if(NIR ==1) {sec1:=81;sec2:=12;};
if(NIR ==2) {sec1:=12;sec2:=23;};
if(NIR ==3) {sec1:=23;sec2:=34;};
if(NIR ==4) {sec1:=34;sec2:=45;};
if(NIR ==5) {sec1:=45;sec2:=56;};
if(NIR ==6) {sec1:=56;sec2:=67;};
if(NIR ==7) {sec1:=67;sec2:=78;};
if(NIR ==8) {sec1:=78;sec2:=81;};

};

mk_apall: macro ={
exec,mk_apir(3,b2);
exec,mk_apir(4,b2);
};

!exec,match_q_qp(b1);
!exec,match_q_qp(b2);

seqedit,sequence=lhcb1;flatten;cycle,start=s.ds.l8.b1;endedit;
seqedit,sequence=lhcb2;flatten;cycle,start=s.ds.l8.b2;endedit;

NRJ=beam%lhcb1->energy;
if(NRJ <3499.99)
{ COmax:=0.004; dPmax=0.00150; VMAXI=30; SPECIF=7; };
if(NRJ >3499.99)
{ COmax:=0.003; dPmax=0.00086; VMAXI=30; SPECIF=7;  };

emittance_norm=3.75e-6;
apbbeat=1.1;
halor=8.4;
halox=7.3;
haloy=7.3;
DParcx=0.273;
DParcy=0.273;



exec,mk_apall(NRJ);



