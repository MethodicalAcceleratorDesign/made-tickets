!--------1---------2---------3---------4---------5---------6---------7---------8 
!-- This job should be run with madX 
! Derived from apr06u.txt 
setplot,ascale=1,lscale=1.5,sscale=3,rscale=1.5,xsize=28,ysize=20, 
post=1,font=-4,lwidth=5; 
setplot,ascale=2,lscale=2,sscale=1.5,rscale=2,xsize=28,ysize=16, 
post=2,font=-4,lwidth=10; 

!-- Define doublet lattice for medical C6+ ring with magnets split into two 
title,"Doublet lattice for 1st H+ ring - apr06v"; 
beam,particle=proton,pc=0.1827,exn=4*0.5e-6,eyn=4*0.5e-6; 
periodL:=0.72; 
periodN:=48; 
fang=-0.07434413245; 
dang:=2*pi/periodN-fang; 
fk1:=+5; 
dk1:=-5; 
bf: sbend,l=0.17,angle=fang,k1:=bfi->k1,e1:=fang/2,e2:=fang/2; 
bd: sbend,l=0.18,angle=dang,k1:=bdi->k1,e1:=dang/2,e2:=dang/2; 
bfi: sbend,l=0.085,angle:=fang/2,k1=25.57051159,e1:=fang/2; 
bdi: sbend,l=0.09,angle:=dang/2,k1=-21.6595443,e1:=dang/2; 
bfo: sbend,l=0.085,angle:=fang/2,k1:=bfi->k1,e2:=fang/2; 
bdo: sbend,l=0.09,angle:=dang/2,k1:=bdi->k1,e2:=dang/2; 
d1: drift,l=0.08; 
d2: drift,l=0.05; 
rfvolt:=0; 
rflag:=0.125; 
rfc: rfcavity,l=0.19,volt:=rfvolt,lag:=rflag,harmon=1; 
col: ecollimator,xsize=0.025,ysize=0.012; 
! dou: line=(col,bf,col,d1,col,bd,col); 
! dou: line=(col,bfi,bfo,col,d1,col,bdi,bdo,col); 
! dou: line=(bfi,bfo,d1,bdi,bdo); 
dou: line=(bf,d1,bd); 
rfs: line=(d2,rfc,d2); 
rfcell: line=(dou,rfs); 
d3: drift,l=0.29; 
norfcell: line=(dou,d3); 
mrk: marker; 
ring: line=(4*(rfcell,11*norfcell),mrk); 

!-- Use twiss for plot of orbit functions 
use,period=rfcell;
select,flag=twiss,column=name,s,betx,alfx,mux,dx,dpx,x,px, 
bety,alfy,muy,dy,dpy,y,py,t,pt,alfa; 
system,"erase apr06v_twiss.lis"; 
twiss,rmatrix,table,file="apr06v_twiss.lis"; 
title," "; 
plot,file="apr06v",haxis=s,vaxis1=betx,bety,vaxis2=dx,colour=100,interpolate, 
noversion,notitle;!,vmin=0.5,0.01,vmax=1.2,0.06; 
use,period=ring; 
system,"erase tmp.lis"; 
select,flag=twiss,column=name,s,betx,alfx,mux,dx,dpx,x,px, 
bety,alfy,muy,dy,dpy,y,py,t,pt,alfa; 
system,"erase apr06v_twiss.lis"; 
twiss,rmatrix,table,file="apr06v_twiss.lis"; 
title," "; 
plot,file="apr06v",haxis=s,vaxis1=betx,bety,vaxis2=dx,colour=100,interpolate, 
noversion,notitle;!,vmin=0.5,0.01,vmax=1.2,0.06; 

!-- Compute fields and gradients 
beam,particle=proton,pc=0.1827,exn=4*0.5e-6,eyn=4*0.5e-6; 
kinE:=beam->energy-beam->mass; 
brho:=beam->pc*1e9/clight/beam->charge; 
value,kinE,brho; 
! fieldF:=bf->angle*brho/bf->l; 
! fieldD:=bd->angle*brho/bd->l; 
! gradF:=bf->k1*brho; 
! gradD:=bd->k1*brho; 
fieldF:=bfi->angle*brho/bfi->l; 
fieldD:=bdi->angle*brho/bdi->l; 
gradF:=bfi->k1*brho; 
gradD:=bdi->k1*brho; 
value,fieldF,fieldD,gradF,gradD; 
value,beam->beta,beam->gamma,beam->beta*beam->gamma; 
value,beam->energy,beam->mass,beam->charge; 
value,2*bfi->angle,2*bdi->angle; 
value,bfi->k1,bdi->k1; 
value,fang,dang; 

!-- Compute SURVEY
use,period=ring;
survey,file="apr06v.srv"; 

!-- Loop over DELTAP
maxdpp=1;
ndpp=0;
dpp=-0.3; 
stepdpp=1/30;
trunc=2.5;
while(ndpp<maxdpp)
{
ndpp=ndpp+1;

!-- Read alignment errors, launch PTC_TWISS orbit from perfect machine
use,period=ring;
eoption,add=false,seed=22021955; 
maxorb=50;
norb=0;
while(norb<maxorb)
{
norb=norb+1;
select,flag=error,class=bd;
select,flag=error,class=bf;
ealign,dx:=1e-4*tgauss(trunc),dy:=1e-4*tgauss(trunc);
system,"erase ptc.lis";
ptc_create_universe; 
ptc_create_layout,model=2,method=6,nst=30,exact,time=false; 
ptc_align; 
select,flag=ptc_twiss,column=name,s,beta11,beta12,mu1,disp1, 
beta21,beta22,mu2,disp3,x,px,y,py,t,pt; 
ptc_twiss,closed_orbit,icase=5,file="ptc.lis",deltap=dpp; 
ptc_end; 
if(ndpp == 1 && norb ==1){system,"copy ptc.lis apr06v.lis";}
else{system,"copy /y apr06v.lis+ptc.lis apr06v.lis";} 
}
dpp=dpp+stepdpp;
}

stop; 