!--------1---------2---------3---------4---------5---------6---------7---------8
!-- This job should be run with MadX
!   Derived from nov16r.txt 
amu:=0.931494320;
setplot,ascale=1,lscale=1.5,sscale=3,rscale=1.5,xsize=28,ysize=20,
        post=1,font=-4,lwidth=5;
setplot,ascale=2,lscale=2,sscale=1.5,rscale=2,xsize=28,ysize=20,
        post=2,font=-4,lwidth=10;

!-- Define triplet lattice for gantry
BF: RBEND, L=0.6, ANGLE:=FANG, K1:=FK1;
BD: RBEND, L=0.6, ANGLE:=DANG, K1:=DK1;
RF: RBEND, L=0.6, ANGLE:=-3*FANG, K1:=RFK1;
RD: RBEND, L=0.6, ANGLE:=-3*DANG, K1:=RDK1;
SF: RBEND, L=0.6, K1:=SFK1, ANGLE:=0;
SD: RBEND, L=0.6, K1:=SDK1, ANGLE:=0;
D1: DRIFT, L=0.06;
D2: DRIFT, L:=0.5*PERIODL-BF->L-BD->L-D1->L;
COL: ECOLLIMATOR, XSIZE=0.1, YSIZE=0.1;
MARK: MARKER;
QF: QUADRUPOLE, L=0.100000037761, K1:=QFK1;
QD: QUADRUPOLE, L=0.102870968475, K1:=QDK1;
D1I: DRIFT, L=0.05;
D3I: DRIFT, L=0.050009708435;
D4I: DRIFT, L=0.0;
MID: MARKER;
D3: DRIFT, L=4.963444;
TRI: LINE=(BD,COL,D1,COL,BF,COL);
RTRI: LINE=(RD,COL,D1,COL,RF,COL);
STRI: LINE=(SD,COL,D1,COL,SF,COL);
CELL: LINE=(D2,-TRI,MARK,TRI,D2);
RCELL: LINE=(D2,-RTRI,MARK,RTRI,D2);
SCELL: LINE=(D2,-STRI,MARK,STRI,D2);
INS: LINE=(D3I,QF,D1I,QD);
!GANTRY: LINE=(INS,TRI,D2,8*CELL,D2,-TRI,MID,RTRI,D2,17*RCELL,D2,-RTRI,-INS,D3);
INS2: LINE=(INS,-INS);
PERIODL := 2.58;
PERIODN := 32;
FANG := 6.28318530718/PERIODN/4;
DANG := FANG;
fk1:=1.36;
dk1:=-1.07;
FK2 := 0;
DK2 := 0;
QX := 0.190184157474;
QY := 0.096976707038;
QS := 0.0;
QX' := -0.163247945453;
QY' := -0.18393605486;
ALFX := 0.0;
ALFY := 0.193989082742E-15;
BETX := 0.734826363803;
BETY := 0.734826363798;
X0 := 0.0;
PX0 := 0.0;
Y0 := 0.0;
PY0 := 0.0;
T0 := 0.0;
PT0 := 0.0;
QFK1 := 26.948730866925;
QDK1 := -21.917714109902;
BETA := 0.734826363802;
value,sf->angle,sd->angle;

title,"Triplet lattice for gantry - nov25s";
beam,ex=3.6692e-6,ey=3.6692e-6,sige=0.03;
use,period=cell;

!-- Match and analyse CELL
beta:=3.15054522;
match,sequence=cell,betx=beta,bety=beta;
vary,name=fk1,step=0.1;
vary,name=dk1,step=0.1;
vary,name=beta,step=0.1;
constraint,range=#e,mux=1/4,betx=beta,bety=beta;
lmdif,calls=100,tolerance=1e-20; 
endmatch; 

select,flag=twiss,column=name,s,betx,alfx,mux,dx,dpx,x,px, 
bety,alfy,muy,dy,dpy,y,py,t,pt,alfa; 
system,"rm nov25s_twiss.lis"; 
savebeta,label=atc,place=#e,sequence=cell;
system,"rm twiss.lis";
twiss,rmatrix,table,file="twiss.lis"; 
title," "; 
plot,file="nov25s",haxis=s,vaxis1=betx,bety,vaxis2=dx,colour=100,interpolate, 
noversion,notitle;
system,"cat twiss.lis > nov25s.lis";


!-- Match and analyse RCELL
beta:=3.1654;
use,period=rcell;
rfk1=fk1;
rdk1=dk1;
match,sequence=rcell;
vary,name=rfk1,step=0.1;
vary,name=rdk1,step=0.1;
constraint,range=#e,mux=1/4,muy=atc->muy;
lmdif,calls=100,tolerance=1e-20; 
endmatch; 

select,flag=twiss,column=name,s,betx,alfx,mux,dx,dpx,x,px, 
bety,alfy,muy,dy,dpy,y,py,t,pt,alfa; 
system,"rm twiss.lis";
twiss,rmatrix,table,file="twiss.lis"; 
title," "; 
plot,file="nov25s",haxis=s,vaxis1=betx,bety,vaxis2=dx,colour=100,interpolate, 
noversion,notitle;
system,"cat twiss.lis >> nov25s.lis";


!-- Match and analyse SCELL
value,sf->angle,sd->angle;
beta:=3.04;
use,period=scell;
sfk1=fk1;
sdk1=dk1;
match,sequence=scell;
vary,name=sfk1,step=0.1;
vary,name=sdk1,step=0.1;
constraint,range=#e,mux=1/4,muy=atc->muy;
lmdif,calls=100,tolerance=1e-20; 
endmatch; 
value,sf->angle,sd->angle;

select,flag=twiss,column=name,s,betx,alfx,mux,dx,dpx,x,px, 
bety,alfy,muy,dy,dpy,y,py,t,pt,alfa; 
!system,"rm twiss.lis";
twiss,rmatrix,table,file="twiss.lis"; 
value,sf->angle,sd->angle;
title," "; 
plot,file="nov25s",haxis=s,vaxis1=betx,bety,vaxis2=dx,colour=100,
noversion,notitle;
value,sf->angle,sd->angle;
plot,file="nov25s",haxis=s,vaxis1=betx,bety,vaxis2=dx,colour=100,interpolate, 
noversion,notitle;
system,"cat twiss.lis >> nov25s.lis";
value,sf->angle,sd->angle;

stop;

!-- Define gantry consisting of 2 arcs with opposite deflection
!   at dpp=0 all orbit functions are matched
d4: drift,l=4.39884386;
gantry: line=(4*cell,2*scell,4*rcell,d4);
use,period=gantry;
survey,table,file="nov25s_srv.lis";
plot,table=survey,file="jun24o",haxis=z,vaxis=x,interpolate,noversion;
value,sf->angle,sd->angle;

stop;

!print,clear;
system,"rm gantry.lis";
twiss,betx=atc->betx,bety=atc->bety,dy=atc->dx,file="gantry.lis";
system,"cat gantry.lis >> nov25s.lis";
plot,file="nov25s",haxis=s,vaxis=betx,colour=100,interpolate,noline;
plot,file="nov25s",haxis=s,vaxis=bety,colour=100,interpolate,noline;
plot,file="nov25s",haxis=s,vaxis=dx,colour=100,interpolate,noline;
plot,file="nov25s",haxis=s,vaxis=dx,colour=100,noline;
plot,file="nov25s",haxis=s,vaxis1=betx,bety,vaxis2=dx,colour=100,interpolate,noline; 
plot,file="nov25s",haxis=s,vaxis1=betx,bety,vaxis2=dx,colour=100,noline; 
plot,file="nov25s",haxis=s,vaxis=betx,bety,noversion,interpolate,colour=100,noline; 

!-- Compute fields and gradients
amu:=0.931494320;
value,amu;
beam,particle=ion,mass=12*amu,charge=6,pc=11.42;
use,period=cell;
value,beam->energy,beam->mass,beam->charge;               
kinE:=beam->energy-beam->mass;
emu:=(beam->energy-beam->mass)*amu/beam->mass;
brho:=beam->pc*1e9/clight/beam->charge;
value,kinE,emu,brho;
fieldD=rd->angle*brho/rd->l;
fieldF=rf->angle*brho/rf->l;
gradF=bf->k1*brho;
gradD=bd->k1*brho;
value,fieldF,fieldD,gradF,gradD;
value,beam->beta,beam->gamma,beam->beta*beam->gamma;
value,bf->k1,bd->k1,d2->l;
value,pi;
rhoD=brho/fieldD;
rhoF=brho/fieldF;
value,rhod,rhoF;
Foff=fieldF/gradF;
Doff=fieldD/gradD;
value,doff,foff;
nrhof=bf->k1*rhoF;
nrhoD=bd->k1*rhoD;
value,nrhoF,nrhoD;
value,fk1,dk1,rfk1,rdk1,sfk1,sdk1;
value,sf->angle,sd->angle;

stop;