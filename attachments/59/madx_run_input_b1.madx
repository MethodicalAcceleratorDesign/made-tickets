! ~/mad/lhc/lhc_V6_5_start.madx      generated by C++ MadX_Interface,   edited by hand, removed things not needed to show aperture.c bug
! bug testing:    cd /tmp/$LOGNAME ; /afs/cern.ch/eng/sl/MAD-X/pro/.x86_darwin_90/bin/V5.00/madx < ~/Desktop/madx_run_input_b1.madx ; grep n1min  lhcb1_V6.5_ip5_n1.out
! wromg result     :   @ n1min            %le   6.95322e-310        @ at_element       %09s  "VSSL.1R5"
! correct result   :   @ n1min            %le   5.23114             @ at_element       %15s  "VSSL.1R5.B.B1:1"  from modified aperture.c with const pointer and buffer increase
System,"ln -fns /afs/cern.ch/eng/lhc/optics/V6.503 db";
Option, -echo,-warn,-info;
real const l.mbas2 = 0.0;
real const l.mbcs2 = 0.0;
real const l.mbls2 = 0.0;
real const l.tasb  = 0.0;
real const l.btvss075 = 0.0;
real const l.tctvb = 0;
real const l.x5zdc002 = 0;
real const l.tcddm = 0;
real const l.acnca = 0.0;
real const l.lejl = 0.0;
real const l.mblw = 0.0;
real const l.tcsm = 0.0;
call,file="db/V6.5.seq";
set,format="-18s","11.5g";
call,file="db/V6.5.coll.str";
MomGeV   =3500;
EmitNorm =3.75e-06;
EnerGeV   := sqrt(MomGeV^2+pmass^2); ! energy in GeV
Beam,particle=proton,energy=EnerGeV,bv=+1,sequence=lhcb1;
on_x1   := 0; on_x2   := 0; on_x5   := 0; on_x8   := 0; ! external crossing angles off - needed on to avoid parasitic collisions for 75ns and 25ns spacing
on_sep1 := 0; on_sep2 := 0; on_sep5 := 0; on_sep8 := 0; ! separation off
              on_alice:= 0;               on_lhcb := 0; ! internal spectrometer bumps of. Relevant for IR2, IR8
on_sol_atlas := 0 ; on_sol_alice:= 0 ; on_sol_cms := 0; ! solenoids off, relevant for IR1, IR2, IR5
call, file="db/aperture/layoutapertures.madx";
call, file="db/aperture/exp_pipe_model.madx";
call, file="db/aperture/exp_pipe_install.madx";
call, file="db/aperture/aperture.b1.madx";
call, file="db/aperture/aper_tol.b1.madx";
Option,  echo, warn, info;
select, flag=twiss, clear;
select, flag=twiss, pattern="IP\.*", column=name,s,betx,alfx,mux,dx,dpx,sigx,sigxd,IncX,divx, bety,alfy,muy,dy,dpy,sigy,sigyd,IncY,divy, x,px,y,py, aper_1,aper_2,aper_3,aper_4, nsigx, nsigy; ! select all IPs   and give columns
select, flag=twiss, pattern="LHCINJ\.*";		! select injection
select, flag=twiss, pattern="LHCB1$END";		! select end
select, flag=twiss, pattern="LHCB2$END";		! select end
select, flag=twiss, pattern="s.ds.l5";			! select the start
select, flag=twiss, pattern="e.ds.r5";			! and end of the insertion
use,sequence=lhcb1;
twiss,sequence=lhcb1;
title,"ir5 b1 3.5TeV 3.75mum beta*=0.55m";
set,format="-18s","13.7g";
select, flag=aperture, column=name,s,n1,aper_1,aper_2,aper_3,aper_4,xtol,ytol,rtol,betx,bety,dx,dy,x,y;
aperture, cor=0.004, dp=0.00086, exn=EmitNorm, eyn=EmitNorm, range=s.ds.l5.b1/e.ds.r5.b1, spec=7, interval=1.0, offsetelem="db/aperture/offset.ip5.b1.tfs", file="lhcb1_V6.5_ip5_n1.out";
stop;