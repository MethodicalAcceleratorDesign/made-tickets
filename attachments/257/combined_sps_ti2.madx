
OPTION,  -echo, warn, -info;

SET,     FORMAT="12.6f";


//------------------------- Calculate SPS Twiss ------------------------------

BEAM, PARTICLE = proton, PC = 450;

// 2010 optics
CALL, FILE = "/afs/cern.ch/eng/sps/2010/elements/sps2010.ele";
CALL, FILE = "/afs/cern.ch/eng/sps/2010/sequence/sps2010.seq";
CALL, FILE = "/afs/cern.ch/eng/sps/2010/strength/lhc_newwp_2010.str" ; !Q26
!call, file = "/afs/cern.ch/eng/sps/2010/strength/SPS_Q20.str";          !Q20
CALL, FILE = "/afs/cern.ch/eng/sps/2010/strength/elements.str" ;

OPTION,  -echo, warn, -info;


QF.60010.E:MARKER;
QD.61110.E:MARKER;
QDA.61910.E:MARKER;
QDA.61910.S:MARKER;

TPSG.61773.E:MARKER;
TPSG.61776.S:MARKER;

QF.61410.E:MARKER;
QF.61410.S:MARKER;
QD.61510.E:MARKER;
QD.61510.S:MARKER;
QF.61610.E:MARKER;
QF.61610.S:MARKER;
QDA.61710.E:MARKER;
QDA.61710.S:MARKER;
QFA.61810.E:MARKER;
QFA.61810.S:MARKER;


SEQEDIT, SEQUENCE = SPS; 
	INSTALL, ELEMENT = QF.60010.E, AT = 5759.5865, FROM = BEGI.10010;
	INSTALL, ELEMENT = QD.61110.E, AT = -QD.61110->L/2, FROM = QD.61110;
	INSTALL, ELEMENT = QDA.61910.E, AT =-QDA.61910->L/2, FROM = QDA.61910;
	INSTALL, ELEMENT = QDA.61910.S, AT = QDA.61910->L/2, FROM = QDA.61910;

	INSTALL, ELEMENT = TPSG.61773.E, AT = 6321.9987, FROM = BEGI.10010;// 6321.9987 position slightly (~55mm) different from 2006 optics
	INSTALL, ELEMENT = TPSG.61776.S, AT = 6325.8047, FROM = BEGI.10010;// 6325.8047 position slightly (~50mm) different from 2006 optics

	INSTALL, ELEMENT=QF.61410.E, AT=-QF.61410->L/2, FROM=QF.61410;
	INSTALL, ELEMENT=QF.61410.S, AT= QF.61410->L/2, FROM=QF.61410;
	INSTALL, ELEMENT=QD.61510.E, AT=-QD.61510->L/2, FROM=QD.61510;
	INSTALL, ELEMENT=QD.61510.S, AT= QD.61510->L/2, FROM=QD.61510;
	INSTALL, ELEMENT=QF.61610.E, AT=-QF.61610->L/2, FROM=QF.61610;
	INSTALL, ELEMENT=QF.61610.S, AT= QF.61610->L/2, FROM=QF.61610;
	INSTALL, ELEMENT=QDA.61710.E, AT=-QDA.61710->L/2, FROM=QDA.61710;
	INSTALL, ELEMENT=QDA.61710.S, AT= QDA.61710->L/2, FROM=QDA.61710;
	INSTALL, ELEMENT=QFA.61810.E, AT=-QFA.61810->L/2, FROM=QFA.61810;
	INSTALL, ELEMENT=QFA.61810.S, AT= QFA.61810->L/2, FROM=QFA.61810;

	FLATTEN; 
ENDEDIT;


USE,     SEQUENCE=sps; 

SAVEBETA, LABEL = QF.60010.BETA, PLACE = QF.60010.E;
SELECT, FLAG=TWISS, CLEAR;
SELECT, FLAG=TWISS, COLUMN=name,L,s,betx,bety,alfx,alfy,dx,dpx,dy,dpy,x,px,y,py,mux,muy;
TWISS, SAVE, FILE="twiss.sps.tfs";



VALUE, QD.61110->K1;
VALUE, QF.61210->K1;

SHOW, QF.60010.BETA;

// ------------- extract LSS6 ---------------------

QF.60010.BETA->mux = 0.0;
QF.60010.BETA->muy = 0.0;
SHOW, QF.60010.BETA;


// Replace QDA.61910 by the coil window

k.coil = -0.16*(kQD)*9./11;
k.coil.a = k.coil;
k.coil.b = k.coil;
k.coil.c = k.coil;
k.coil.d = k.coil;
k.coil.e = k.coil;

QDACOIL.F:QUADRUPOLE, L=(qda->l)/5;
QDACOIL.a.61910  :  QDACOIL.F,  K1 := k.coil.a;
QDACOIL.b.61910  :  QDACOIL.F,  K1 := k.coil.b;
QDACOIL.c.61910  :  QDACOIL.F,  K1 := k.coil.c;
QDACOIL.d.61910  :  QDACOIL.F,  K1 := k.coil.d;
QDACOIL.e.61910  :  QDACOIL.F,  K1 := k.coil.e;

SEQEDIT, SEQUENCE=SPS;
	REMOVE, ELEMENT=QDA.61910;
	INSTALL, ELEMENT=QDACOIL.a.61910, AT=6369.0854-2*qdacoil.f->l;
	INSTALL, ELEMENT=QDACOIL.b.61910, AT=6369.0854-qdacoil.f->l; 
	INSTALL, ELEMENT=QDACOIL.c.61910, AT=6369.0854;
	INSTALL, ELEMENT=QDACOIL.d.61910, AT=6369.0854+qdacoil.f->l;
	INSTALL, ELEMENT=QDACOIL.e.61910, AT=6369.0854+2*qdacoil.f->l;
ENDEDIT;

SEQEDIT, SEQUENCE=SPS;
	FLATTEN; 
ENDEDIT;


VALUE, k.coil;
SHOW, QDACOIL.a.61910;


EXTRACT, SEQUENCE=SPS, FROM=QF.60010.E, TO=QDA.61910.S, NEWNAME=SPS6, REFPOS=QF.60010.E; 

sps6length = QDA.61910.S->at-QF.60010.E->at;
VALUE, sps6length;





// ------------- combine with TI 2 -----------------

CALL, FILE='/afs/cern.ch/eng/lhc/optics/TI2/TI2.seq';
!CALL, FILE='../TI2.seq';
CALL, FILE='/afs/cern.ch/eng/lhc/optics/TI2/TI2.str'; !Q26
!CALL, FILE='/afs/cern.ch/eng/lhc/optics/TI2/Q20/q20_ti2_old_colli.str' !q20?

TI2.START:MARKER;
QTLD.610100.E:MARKER;
QTLD.610100.S:MARKER;
QTLF.610200.E:MARKER;
QTLF.610200.S:MARKER;
MDLH.610206.S:MARKER;
TI2.END:MARKER;
SEQEDIT, SEQUENCE=TI2;
	INSTALL, ELEMENT=TI2.START, AT=0;
	INSTALL, ELEMENT=QTLD.610100.E, AT=-QTLD.610100->L/2, FROM=QTLD.610100;
	INSTALL, ELEMENT=QTLD.610100.S, AT= QTLD.610100->L/2, FROM=QTLD.610100;
	INSTALL, ELEMENT=QTLF.610200.E, AT=-QTLF.610200->L/2, FROM=QTLF.610200;
	INSTALL, ELEMENT=QTLF.610200.S, AT= QTLF.610200->L/2, FROM=QTLF.610200;
	INSTALL, ELEMENT=MDLH.610206.S, AT=MDLH.610206->L/2, FROM=MDLH.610206;
	INSTALL, ELEMENT=TI2.END, AT=0, FROM=TI2$END;
ENDEDIT;

ti2length=TI2.END->AT-TI2.START->at;
VALUE, ti2length;//=  3188.384281

LFULL = ti2length + sps6length + 0.00457; //=  3188.384281 + 611.3944 + 0.00457     

!all: SEQUENCE, REFER=CENTRE, L=LFULL;                              
all: SEQUENCE, REFER=ENTRY, L=LFULL; 
	sps6, at=0.0;
	ti2,  at=sps6length+0.00457;       
ENDSEQUENCE;

EXTRPT:MARKER;
SEQEDIT, SEQUENCE=all;
	INSTALL, ELEMENT=EXTRPT, AT=0.00457, FROM=QDA.61910.S;
ENDEDIT;

SEQEDIT, SEQUENCE=all;
	FLATTEN; 
ENDEDIT;

use,sequence=all;

SELECT, FLAG=TWISS, CLEAR;
SELECT, FLAG=TWISS, COLUMN=name,L,s,betx,bety,alfx,alfy,dx,dpx,dy,dpy,x,px,y,py,mux,muy;
TWISS, beta0=QF.60010.BETA,SAVE, FILE="twiss.all.tfs";

stop;
