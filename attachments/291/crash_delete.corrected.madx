Option, warn,info,echo;

Title, "LHC 6.5 with errors and orbit correction using MADX";

// CALL IN SELECTION, SEQUENCE and OPTICS FILES;
Option, -echo, -warn, -info;
Call, file="/afs/cern.ch/eng/lhc/optics/V6.503/V6.5.seq";
Call, file="/afs/cern.ch/eng/lhc/optics/V6.503/V6.5.inj.str";
Option, warn, echo, -info;

Beam, particle=proton, sequence=lhcb1, energy=450.0, NPART=1.05E11, sige=4.5e-4 ;

// First make a clean machine as the model ...
Use, period=lhcb1;
select, flag=twiss, clear;
select, flag=twiss, column=name,s,x,px,y,py,betx,bety,alfx,alfy;
TWISS,table=my_model,sequence=lhcb1; 

round1(zzz): macro={
	! Orbit generation and correction
	Use, period=lhcb1;

	option, info;
	Select, flag=ERROR, clear;
	Select, flag=error, pattern="MQ\.31L3\.B1";
	ealign, dy=1.e-3;

	s1 = 22021955*ranf(); value, s1;
	eoption, add=true, seed=s1;
	Select, flag=ERROR, clear;
	Select, flag=ERROR, class=MQ;
	ealign, dy:=tgauss(3.0)*0.100e-3, dx:=tgauss(3.0)*0.100e-3;
	option, -info;
	
	select, flag=twiss, clear;
	select, flag=twiss, column=name,s,x,px,y,py,betx,bety,alfx,alfy;
	twiss, table=my_model,sequence=lhcb1;
	plot, table=twiss, title="zzzX plane", noline, vmin=-0.012, vmax=0.012, haxis=s, vaxis=x;
	plot, table=twiss, title="zzzY plane", noline, vmin=-0.012, vmax=0.012, haxis=s, vaxis=y;

	// Prepare first correction to zero orbit
	USEKICK,CLASS=MBLW, status=OFF; ! IR8 spectrometer magnets
	USEKICK,CLASS=MBXWH, status=OFF; ! IR8 spectrometer magnets
	USEKICK,CLASS=MBXWS, status=OFF; ! IR8 spectrometer magnets
	USEKICK,CLASS=MBXWT, status=OFF;
	USEKICK,CLASS=MSDB, status=OFF; 
	USEKICK,CLASS=MKA, status=OFF; 
	USEKICK,CLASS=MCBYH, status=OFF; 
	USEKICK,CLASS=MCBYV, status=OFF; 
	! USEKICK,CLASS=MCBX, status=OFF; 
	USEKICK,class=MKI, status=OFF;
	USEKICK,class=MKI__192, status=OFF;
	USEKICK,class=MKI__193, status=OFF;
	USEKICK,class=MKA, status=OFF;
	USEKICK,class=MKD, status=OFF;
	USEKICK,class=MKQ, status=OFF;
	USEKICK,class=MSDA, status=OFF;
	USEKICK,class=MSDB, status=OFF;
	USEKICK,class=MSDB2, status=OFF;
	USEKICK,class=MSDC, status=OFF;
	USEKICK,class=MSIA, status=OFF;
	USEKICK,class=MSIB, status=OFF;
	USEKICK,class=MBXWS, status=OFF;  
	USEKICK,RANGE=MBAW.1R2, status=OFF; ! IP2 spectrometer compensation
	USEKICK,RANGE=MBWMD.1L2, status=OFF; ! IP2 spectrometer compensation

                    print, text="CORRECT 1";
	CORRECT, FLAG=ring, model=my_model,
        	MODE=micado, cond=1, NCORR=100, ERROR=1E-6, PLANE=y,
 		 mlist='myzzz.tab', clist='cyzzz.tab', corzero=1;
                    print, text="CORRECT 2";
                    CORRECT, FLAG=ring, model=my_model,
        	   MODE=micado, cond=1, NCORR=100, ERROR=1E-6, PLANE=x,
                                         mlist='mxzzz.tab', clist='cxzzz.tab';
 
};

mergemon(zzz): macro={
	     create, table=monzzz, column=_name,x,y;

	     readmytable, file="mxzzz.tab", table=MXzzz;
	     readmytable, file="myzzz.tab", table=MYzzz;

	     n_elem  =  table(MXzzz, tablelength);

	     i = 0;
	     while(i < n_elem) {
	          i = i + 1;
	          setvars, table=MXzzz, row=i; xsave=x;		        
	     	  setvars, table=MYzzz, row=i; x=xsave;
		  fill, table=monzzz;   		  
	     };
	     
	     write, table=monzzz, file="mon_zzz.tfs";

	     print, text="===Delete table monzzz";
	     delete, table=monzzz;

	     print, text="===Delete table MXzzz";
	     delete, table=MXzzz;

	     print, text="===Delete table MYzzz";
	     delete, table=MYzzz
};

mergecor(zzz): macro={
	     create, table=corzzz, column=_name,px,py;

	     readmytable, file="cxzzz.tab", table=CXzzz;
	     readmytable, file="cyzzz.tab", table=CYzzz;

	     n_elem  =  table(CXzzz, tablelength);

	     i = 0;
	     while(i < n_elem) {
	          i = i + 1;
	          setvars, table=CXzzz, row=i; 
	          pxsave=px.correction; 
	          namex=name;	        
	          
	          value, namex;
	          
	          setvars, table=CYzzz, row=i; 
	          pysave=py.correction; 
	          namey=name;
	          value, namey;
                              
	          px=pxsave; py=pysave;
                              fill, table=corzzz;   		  
	     };
	     
	     write, table=corzzz, file="cor_zzz.tfs";



	     print, text="<<< Delete table CYzzz";
	     delete, table=CYzzz;

	     print, text="<<< Delete table CXzzz";
	     delete, table=CXzzz;


	     print, text="<<< Delete table corzzz";
	     delete, table=corzzz; ! crashes MAD-X

};

cleanup(zzz): macro={
	     removefile, file="cxzzz.tab";
   	     removefile, file="cyzzz.tab";
	     removefile, file="mxzzz.tab";
	     removefile, file="myzzz.tab";
};



count = 0;
While (count < 4) {
      count = count + 1;
      value, count;
      exec, round1($count);
      exec, mergemon($count);
      exec, mergecor($count);
      exec, cleanup($count);
      removefile, file="fort.61";
};

stop;

